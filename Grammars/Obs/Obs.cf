(:). [ObsId] ::= "@@@" ObsId [ObsId];
(:[]). [ObsId] ::= "@@@" ObsId;


ObsId. ObsId ::= ThreadId Obs; 
ObsName. Obs ::= "NAME" ObsIdent;
ObsInit. Obs ::= "INIT";
ObsTask. Obs ::= "TASK" Taskname;
ObsSignal. Obs ::= "SIGNAL" Integer;
ObsDef. Obs ::= "DEF" Varname Varval;
ObsDecl. Obs ::= "DECL" Typename Varname;
ObsDeclVal. Obs ::= "DECL" Typename Varname Varval;
ObsDeclArr. Obs ::= "DCLARRAY" Typename Varname SizeDcl;
ObsCall. Obs ::= "CALL" ObsIdent Args;
ObsState. Obs ::= "STATE" Integer StateObs;
ObsStruct. Obs ::= "STRUCT" Varname;
ObsSeq. Obs ::= "SEQ" Varname Scalar;
ObsPtr. Obs ::= "PTR" Varname Varval;
ObsScalar. Obs ::= "SCALAR" Varname Varval;
ObsScalarIndex. Obs ::= "SCALAR" Varname Varindex Varval;
ObsEnd. Obs ::= "END" Varname;

ObsScalarNone. Scalar ::= End;
ObsScalarCons. Scalar ::= "@@@" ThreadId "SCALAR" "_" Varval Scalar;

ObsEndSeq. End ::= "@@@" ThreadId "END" Varname;

ArgsConsString. Args ::= ObsStr Args;
ArgsConsInteger. Args ::= Integer Args;
ArgsConsDouble. Args ::= Double Args;
ArgsOne. Args ::= ;

TaskName. Taskname ::= ObsIdent;
VarName. Varname ::= ObsIdent;
VarValInt. Varval ::= Integer;
VarValDouble. Varval ::= Double;
VarValNull. Varval ::= "NULL";
VarValStr. Varval ::= ObsStr;
TypeName. Typename ::= ObsIdent;
StateObs. StateObs ::= ObsIdent;
VarIndex. Varindex ::= Integer;
ThreadId. ThreadId ::= Integer;
SizeDclVar. SizeDcl ::= Varval;
SizeDclDef. SizeDcl ::= ObsIdent;

token ObsIdent (letter | '_' ) (digit | letter | '_')*;
token ObsStr (letter | '_' | '.' | ',' |':')+; 