(:). [Obs] ::= "@@@" Obs [Obs];
(:[]). [Obs] ::= "@@@" Obs;

ObsName. Obs ::= ThreadId "NAME" ObsIdent;
ObsInit. Obs ::= ThreadId "INIT";
ObsTask. Obs ::= ThreadId "TASK" Taskname;
ObsSignal. Obs ::= ThreadId "SIGNAL" Integer;
ObsDef. Obs ::= ThreadId "DEF" Varname Varval;
ObsDecl. Obs ::= ThreadId "DECL" Typename Varname;
ObsDeclVal. Obs ::= ThreadId "DECL" Typename Varname Varval;
ObsDeclArr. Obs ::= ThreadId "DCLARRAY" Typename Varname SizeDcl;
ObsCall. Obs ::= ThreadId "CALL" ObsIdent Args;
ObsState. Obs ::= ThreadId "STATE" Integer StateObs;
ObsStruct. Obs ::= ThreadId "STRUCT" Varname;
ObsSeq. Obs ::= ThreadId "SEQ" Varname Scalar;
ObsPtr. Obs ::= ThreadId "PTR" Varname Varval;
ObsScalar. Obs ::= ThreadId "SCALAR" Varname Varval;
ObsScalarIndex. Obs ::= ThreadId "SCALAR" Varname Varindex Varval;
ObsEnd. Obs ::= ThreadId "END" Varname;

ObsScalarNone. Scalar ::= End;
ObsScalarCons. Scalar ::= "@@@" ThreadId "SCALAR" "_" Varval Scalar;

ObsEndSeq. End ::= "@@@" ThreadId "END" Varname;

ArgsConsString. Args ::= ObsStr Args;
ArgsConsInteger. Args ::= Integer Args;
ArgsConsDouble. Args ::= Double Args;
ArgsOne. Args ::= ;

TaskName. Taskname ::= ObsIdent;
VarName. Varname ::= ObsIdent;
VarValInt. Varval ::= Integer;
VarValDouble. Varval ::= Double;
VarValNull. Varval ::= "NULL";
VarValStr. Varval ::= ObsStr;
TypeName. Typename ::= ObsIdent;
StateObs. StateObs ::= ObsIdent;
VarIndex. Varindex ::= Integer;
ThreadId. ThreadId ::= Integer;
SizeDclVar. SizeDcl ::= Varval;
SizeDclDef. SizeDcl ::= ObsIdent;

token ObsIdent (letter | '_' ) (digit | letter | '_')*;
token ObsStr (letter | '_' | '.' | ',' |':')+; 