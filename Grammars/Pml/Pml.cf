terminator nonempty Module "";
rules Visible ::= "hidden" | "show";
rules ChanPoll ::= "full" | "empty" "nfull" "nempty";
rules Separator ::= ";" | "->";
rules Typename ::= "bit" | "bool" | "byte" | "short" | "int" | "mtype" | "chan" | "pid" | PIdent;
rules UnrOp ::= "~" | "-" | "!";
rules Const ::= "true" | "false" | "skip" | Integer;
rules PrintType ::= "print" | "printf" | "printm";

Mproc. Module ::= Proctype;
Minline. Module ::= Inline;
Minit. Module ::= Init;
Mnever. Module ::= Never;
Mtrace. Module ::= Trace;
Mutype. Module ::= Utype;
Mmtype. Module ::= Mtype;
MdeclList. Module ::= DeclList;

Ptype. Proctype ::= Pactive "proctype" PIdent "(" PdeclList ")" Ppriority Penabler "{" Sequence "}";
Iline. Inline ::= "inline" PIdent "(" [AnyExpr] ")" "{" Sequence "}";

PactiveNone. Pactive ::= ; 
PactiveOne. Pactive ::= Active;

PdeclListNone. PdeclList ::= ;
PdeclListOne. PdeclList ::= DeclList;

PpriorityNone. Ppriority ::= ;
PpriorityOne. Ppriority ::= Priority;

PenablerNone. Penabler ::= ;
PenablerOne. Penabler ::= Enabler;

Initialise. Init ::= "init" Ipriority "{" Sequence "}" ;

IpriorityNone. Ipriority ::= ;
IpriorityOne. Ipriority ::= Priority;

Nvr. Never ::= "never" "{" Sequence "}";
Trc. Trace ::= "trace" "{" Sequence "}";

Utp. Utype ::= "typedef" PIdent "{" DeclList "}" ";";

MtpEq. Mtype ::= "mtype" Mequals "{" [PIdent] "}" Msep;
MtpNoEq. Mtype ::= "mtype" [PIdent] Msep;
MsepNone. Msep ::= ;
MsepOne. Msep ::= ";";
Meq. Mequals ::= ;
Meq. Mequals ::= "=";

(:[]). [PIdent] ::= PIdent;
(:). [PIdent] ::= PIdent "," [PIdent];

DclListOneNoSep. DeclList ::= Decl;
DclListOne. DeclList ::= Decl Separator;
DclListCons. DeclList ::= Decl Separator DeclList;

DclOne. Decl ::= DeclVisible Typename [Ivar];
DclOneUnsigned. Decl ::= DeclVisible UnsignedDecl;

(:). [Ivar] ::= Ivar "," [Ivar];
(:[]). [Ivar] ::= Ivar;

DclVisNone. DeclVisible ::= ;
DclVisOne. DeclVisible ::= Visible;

UDcl. UnsignedDecl ::= "unsigned" PIdent ":" Const UDclAssign;
UDclAssignNone. UDclAssign ::= ;
UdclAssignOne. UDclAssign ::= "=" AnyExpr;

Active. Active ::= "active" AConst;

AconstNone. AConst ::= ;
AconstOne. AConst ::= "[" Const "]";

Priority. Priority ::= "priority" Const;
Enabler. Enabler ::= "provided" "(" Expr ")";

SeqOne. Sequence ::= Step;
SeqOneSep. Sequence ::= Step Separator;
SeqNoSep. Sequence ::= Step Sequence;
SeqCons. Sequence ::= Step Separator Sequence;

UStmtNone. UStmt ::= ;
UStmtOne. UStmt ::= "unless" Stmt;

StepMType. Step ::= Mtype;
StepDclList. Step ::= DeclList;
StepStmt. Step ::= Stmt UStmt;
StepXR. Step ::= "xr" VarRefList;
StepXS. Step ::= "xs" VarRefList;

VarRefListOne. VarRefList ::= VarRef;
VarRefListCons. VarRefList ::= VarRef "," VarRefList;

AnyExprCond.      AnyExpr ::= "(" AnyExpr2 "->" AnyExpr2 ":" AnyExpr2 ")";
AnyExprlor.       AnyExpr2 ::= AnyExpr2 "||" AnyExpr3;
AnyExprland.      AnyExpr2 ::= AnyExpr2 "&&" AnyExpr3;
AnyExprbitor.     AnyExpr3 ::= AnyExpr3 "|" AnyExpr4;
AnyExprbitxor.    AnyExpr3 ::= AnyExpr3 "^" AnyExpr4;
AnyExprbitand.    AnyExpr3 ::= AnyExpr3 "&" AnyExpr4;
AnyExpreq.        AnyExpr4 ::= AnyExpr4 "==" AnyExpr5;
AnyExprneq.       AnyExpr4 ::= AnyExpr4 "!=" AnyExpr5;
AnyExprlthan.     AnyExpr5 ::= AnyExpr5 "<" AnyExpr6;
AnyExprgrthan.    AnyExpr5 ::= AnyExpr5 ">" AnyExpr6;
AnyExprle.        AnyExpr5 ::= AnyExpr5 "<=" AnyExpr6;
AnyExprge.        AnyExpr5 ::= AnyExpr5 ">=" AnyExpr6;
AnyExprleft.      AnyExpr6 ::= AnyExpr6 "<<" AnyExpr7;
AnyExprright.     AnyExpr6 ::= AnyExpr6 ">>" AnyExpr7;
AnyExprplus.      AnyExpr7 ::= AnyExpr7 "+" AnyExpr8;
AnyExprminus.     AnyExpr7 ::= AnyExpr7 "-" AnyExpr8;
AnyExprtimes.     AnyExpr8 ::= AnyExpr8 "*" AnyExpr9;
AnyExprdiv.       AnyExpr8 ::= AnyExpr8 "/" AnyExpr9;
AnyExprmod.       AnyExpr8 ::= AnyExpr8 "%" AnyExpr9;
AnyExprUnrOp. AnyExpr8 ::= UnrOp AnyExpr9;
AnyExprLen. AnyExpr9 ::= "len" "(" VarRef ")";
AnyExprPoll. AnyExpr9 ::= Poll;
AnyExprVarRef. AnyExpr9 ::= VarRef;
AnyExprConst. AnyExpr9 ::= Const;
AnyExprTimeout. AnyExpr9 ::= "timeout";
AnyExprNp. AnyExpr9 ::= "np_";
AnyExprEnabled. AnyExpr9 ::= "enabled" "(" AnyExpr ")";
AnyExprPCValue. AnyExpr9 ::= "pc_value" "(" AnyExpr ")";
AnyExprName. AnyExpr9 ::= PIdent "[" AnyExpr "]" "@" PIdent;
AnyExprRun. AnyExpr9 ::= "run" PIdent "(" RunArgs ")" RunPrio;
AnyExprGetPrio. AnyExpr9 ::= "get_priority" "(" Expr ")";
AnyExprSetPrio. AnyExpr9 ::= "set_priority" "(" Expr "," Expr ")";

_. AnyExpr   ::= AnyExpr1 ;
_. AnyExpr1  ::= AnyExpr2 ;
_. AnyExpr2  ::= AnyExpr3 ;
_. AnyExpr3  ::= AnyExpr4 ;
_. AnyExpr4  ::= AnyExpr5 ;
_. AnyExpr5  ::= AnyExpr6 ;
_. AnyExpr6  ::= AnyExpr7 ;
_. AnyExpr7  ::= AnyExpr8 ;
_. AnyExpr8  ::= AnyExpr9 ;
_. AnyExpr9  ::= "(" AnyExpr ")";

Ivar. Ivar ::= PIdent IvarConst IvarAssign;
IvarConstNone. IvarConst ::= ; 
IvarConstOne. IvarConst ::= "[" Const "]";
IvarAssignNone. IvarAssign ::= ;
IvarAssignAnyExpr. IvarAssign ::= "=" AnyExpr;
IvarAssignChInit. IvarAssign ::= "=" ChInit;

ChInit. ChInit ::= "[" Const "]" "of" "{" [Typename] "}";

(:[]). [Typename] ::= Typename;
(:). [Typename] ::= Typename "," [Typename];

VarRef. VarRef ::= PIdent VarRefAnyExpr VarRefTypedef;
VarRefAnyExprNone. VarRefAnyExpr ::= ;
VarRefAnyExprOne. VarRefAnyExpr ::= "[" AnyExpr "]";
VarRefTypedefNone. VarRefTypedef ::= ;
VarRefTypedefOne. VarRefTypedef ::= "." VarRef;

SendNormal. Send ::= VarRef "!" SendArgs;
SendSorted. Send ::= VarRef "!" "!" SendArgs;

ReceiveNormal. Receive ::= VarRef "?" RecvArgs;
ReceiveRandom. Receive ::= VarRef "?" "?" RecvArgs;
ReceivePoll. Receive ::= VarRef "?" "<" RecvArgs ">";
ReceivePollSecond. Receive ::= VarRef "?" "?" "<" RecvArgs ">";

PollNoSideEffect. Poll ::= VarRef "?" "[" RecvArgs "]";
PollNoSideEffectSecond. Poll ::= VarRef "?" "?" "[" RecvArgs "]";

SendArgs. SendArgs ::= [AnyExpr];
SendArgsExpr. SendArgs ::= AnyExpr "(" [AnyExpr] ")";

(:). [AnyExpr] ::= AnyExpr "," [AnyExpr];
(:[]). [AnyExpr] ::= AnyExpr;
[]. [AnyExpr] ::= ;

RecvArgsList. RecvArgs ::= [RecvArg];
RecvArgsParen. RecvArgs ::= [RecvArg] "(" RecvArgs ")";

(:[]). [RecvArg] ::= RecvArg;
(:). [RecvArg] ::= RecvArg [RecvArg];

UnaryMinusNone. UnaryMinus ::= ;
UnaryMinusOne. UnaryMinus ::= "-";

RecvArgRef. RecvArg ::= VarRef;
RecvArgEval. RecvArg ::= "eval" "(" VarRef ")";
RecvArgConst. RecvArg ::= UnaryMinus Const;

AssignStd. Assign ::= VarRef "=" AnyExpr;
AssignInc. Assign ::= VarRef "++";
AssignDec. Assign ::= VarRef "--";

PArgsString. Pargs ::= String;
PArgsNoString. Pargs ::= [AnyExpr];
PArgsBoth. Pargs ::= String "," [AnyExpr];

StmtIf. Stmt ::= "if" Options "fi";
StmtDo. Stmt ::= "do" Options "od";
StmtFor. Stmt ::= "for" "(" Range ")" "{" Sequence "}";
StmtAtomic. Stmt ::= "atomic" "{" Sequence "}";
StmtDAtomic. Stmt ::= "d_step" "{" Sequence "}";
StmtSelect. Stmt ::= "select" "(" Sequence ")";
StmtNorm. Stmt ::= "{" Sequence "}";
StmtSend. Stmt ::= Send;
StmtRec. Stmt ::= Receive;
StmtAssign. Stmt ::= Assign;
StmtElse. Stmt ::= "else";
StmtBreak. Stmt ::= "break";
StmtGoto. Stmt ::= "goto" PIdent;
StmtLabel. Stmt ::= PIdent ":" Stmt;
StmtPrint. Stmt ::= PrintType "(" Pargs ")";
StmtAssert. Stmt ::= "assert" Expr;
StmtCall. Stmt ::= PIdent "(" [AnyExpr] ")";
StmtExpr. Stmt ::= Expr;

RangeIn. Range ::= PIdent "in" PIdent;
RangeNoIn. Range ::= PIdent ":" AnyExpr ".." AnyExpr;

OptionsOne. Options ::= "::" Sequence;
OptionsCons. Options ::= "::" Sequence Options;

RunPrioNone. RunPrio ::= ;
RunPrioOne. RunPrio ::= Priority;

RunArgsNone. RunArgs ::= ;
RunArgsOne. RunArgs ::= [AnyExpr];

ExprAny. Expr ::= AnyExpr;
ExprParen. Expr ::= "(" Expr ")";
ExprChanPoll. Expr ::= ChanPoll "(" VarRef ")";

token PIdent (letter | '_') (letter | '_' | digit)*;